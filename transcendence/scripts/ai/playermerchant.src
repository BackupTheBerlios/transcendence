use npc;
use basic;
use os;
use uo;
use util;

include "include/eventid";
include "include/attributes";
include "include/randname";
include "include/utility";
include "include/mrcspawn";
include "include/dist";
include "include/client";
include "include/npcbackpacks";
include "include/yesno";

const UOBJ_GOLD_COIN := 0xeed;
const STARTHAIR := 0x2030;
const ENDHAIR := 0x2060;

set_priority( 50 );

var stor, inv_fs, inv_pb;
var me := Self();
var master := CInt(GetObjProperty(me, "master"));
var mastername := GetObjProperty(me, "mn");
var cont := GetObjProperty(me, "cont");

if( me.name["<random>"] )
   	case( RandomInt(2) )
      	0: me.graphic := 400;
      	1: me.graphic := 401;
   	endcase
	SetName( me, RandomName( me ) );
	EquipFromTemplate( me, GetObjProperty( me, "Equipt") );
endif

program merchant()
        print("VENDOR STARTED");
	if (master)
		var newname := SplitWords(me.name);
		if (lower(newname[2]) == "the")
			me.name := newname[1] + ", " + mastername + "'s Merchant";
		endif
	endif

	stor := FindStorageArea( "Merchant Storage" );
	if (!stor)
		exit;
	endif

	inv_fs := find_or_create_item( stor, me.serial + " FS", 0xe7c );
	inv_pb := find_or_create_item( stor, me.serial + " PB", 0xe7c );
	inv_fs := find_or_create_item( stor, me.serial + " 1C", 0xe7c );

	if (!inv_fs || !inv_pb || !inv_fs)
    		print("PLAYERENDOR EXITED!!!!!!!!!!!!!!!");
    		exit;
	endif

	EnableEvents( EVID_SPEECH + EVID_ITEM_GIVEN + EVID_MERCHANT_SOLD  );

	var res;
	var ev;

	var nextpay := GetObjProperty(me,"nextpay");
	if (!nextpay)
		nextpay := ReadGameClock();
		SetObjProperty(me,"nextpay", nextpay);
	endif

    while (1)
    print("VENDOR CYCLE");
    ev := os::wait_for_event( 4 );
    if (ev)
    print("VENDOR EVENT");
        case (ev.type)
            EVID_SPEECH:
                if (ev.text["buy"])
                    TurnToward( ev.source );
		    if (!master)
			say("I am currently unemployed.  Want to hire me?");
		    else
                        res := SendBuyWindow( ev.source, inv_fs, Self(), inv_pb );
                    endif
                elseif (ev.text["purchase"])
			if (ev.source.serial == master)
                        	addthingstobuy();
			endif
                elseif (ev.text["release"])
                    TurnToward( ev.source );
                    if (ev.source.serial == master)
			Fireme(ev);
                    else
                        say("I don't even work for you.");
                    endif
                elseif (ev.text["sell"])
			TurnToward( ev.source );
			SpoutBuyList(ev.source);
                elseif (ev.text["contact"])
                        TurnToward( ev.source );
                        if (ev.source.serial == master)
                                cont := recontact(ev.source);
                                SetObjProperty(me, "cont", cont);
                        else
                                say("You may contact my employer " + cont);
                        endif
                elseif ((ev.text["collect"]) && (ev.source.serial == master))
                        TurnToward( ev.source );
                        cashout();
                elseif ((ev.text["status"]) && (ev.source.serial == master))
                        TurnToward( ev.source );
                        statuscheck();
                elseif ((ev.text["wear"]) && (ev.source.serial == master))
                        TurnToward( ev.source );
                        say("What shall I wear?");
                        var this := Target(ev.source);
                        wear_item( this);
                elseif ((ev.text["strip"]) && (ev.source.serial == master))
                        TurnToward( ev.source );
                        say("Ooh, I think I feel a draft!");
			strip_items(ev.source);
                elseif ((ev.text["follow"]) && (ev.source.serial == master))
			say("Lead the way!");
			Follow(ev.source);
                elseif (ev.text["hire"])
                        TurnToward( ev.source );
                        if (!master)
                                say("For 1000 gold to start, plus 50 per day, I'll work for a 5% commission");
                        endif
                elseif (ev.text["help"])
                        TurnToward( ev.source );
			say("I will obey the following orders:");
			sleep(1);
			say("release, sell, contact, collect,");
			sleep(2);
			say("wear, strip, follow, stop, hire, status and purchase");
                endif

            EVID_MERCHANT_SOLD:
                TurnToward( ev.source );
                PrintTextAbovePrivate( Self(), "The total of your purchase is " + ev.amount, ev.source );
		TakeSale(ev.amount, ev.source.serial);

            EVID_ITEM_GIVEN:
                if (!master)
                        if ((ev.item.objtype == UOBJ_GOLD_COIN) && (ev.item.amount >= 1000))
				HireMe(ev);
				nextpay := ReadGameClock()+86400;
				SetObjProperty(me,"nextpay",nextpay);
			endif
                else
                        if (ev.source.serial == master)
                                stockitem(ev);
                        else
                                buyitem(ev.source, ev.item);
                        endif
                endif
           
        endcase 

	sleep(1);

    endif
      
	if ( ReadGameClock() > nextpay )
		GetPay();
		nextpay := ReadGameClock()+36400;
		SetObjProperty(me,"nextpay",nextpay);
	endif
       
    endwhile

endprogram

function Follow(mmaster)

	var d := Distance(mmaster, me);
	SetDexterityMod(me, 500);
	var ev;

	while ( (mmaster) )
		d:= Distance(mmaster, me);
		if (d > 20)
			PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa, 0, me.realm );
			MoveCharacterToLocation( me, mmaster.x, mmaster.y, mmaster.z );
			PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa, 0, me.realm );
			PlaySoundEffect( mmaster, SFX_SPELL_TELEPORT );
		elseif (d > 1)
			RunToward(mmaster);
		else
			sleep(1);
		endif
		ev := wait_for_event(0);
		if (ev)
			if (ev.type == EVID_SPEECH)
				if ((ev.source.serial == master) && (ev.text["stop"]))
					TurnToward( ev.source );
					say("Ok, I'll just stand right here.");
					return;
				else
					say("I am following you!");
				endif
			else
				say("I am following you!");
			endif
		endif
	endwhile

endfunction

function stockitem(tev)

        if (tev.item.objtype == UOBJ_GOLD_COIN)
		TakeGold(tev);
                return;
        endif

        var item := tev.item;

        SetObjProperty(item, "v", mastername);

	if ( (item.objtype == 0x0e76) || (item.objtype == 0x0e75) ||
		(item.objtype == 0x0e79) || (item.objtype == 0x09aa) || (item.objtype == 0x0e7d) )
		var tname := SendTextEntryGump( tev.source,"How shall I advertise this item?",
                        TE_CANCEL_ENABLE,
                        TE_STYLE_NORMAL);
		if (tname)
			item.name := tname;
		endif
	endif
                   
        var cost :=   CInt(SendTextEntryGump( tev.source, "How much shall I charge for this item? "
, TE_CANCEL_ENABLE,
                        TE_STYLE_NORMAL));
       
        if ( cost > 0 )
                item.sellprice := CInt(cost);
	else
		say("Well, just keep it, then");
		MoveItemToContainer(item,tev.source.backpack);
		return;
        endif

	if (!item.movable)
		item.movable := 1;
	endif

        MoveItemToContainer(item, inv_fs);

        say( "I will sell " + tev.item.desc + " for " + tev.item.sellprice);

endfunction

function cashout()

	var mypay := CInt(GetObjProperty(me, "mypay"));
	var mygold := CInt(GetObjProperty(me, "g"));
	if (mygold >= mypay)
		mypay := 0;
		mygold := mygold - mypay;
	else
		mypay := mypay - mygold;
		mygold := 0;
	endif
	SetObjProperty(me, "mypay",mypay);
	SetObjProperty(me, "g", mygold);

	var mstr := SystemFindObjectBySerial(CInt(master));
	if (mygold)
		var payamt := 0;
		if (mygold > 60000)
			payamt := 60000;
		else
			payamt := mygold;
		endif
		say("Here is your " + payamt );
        	CreateItemInContainer(mstr.backpack, UOBJ_GOLD_COIN, payamt);
		mygold := mygold - payamt;
        	SetObjProperty(me, "g", mygold);
		if (!mygold)
			say("but you have left me nothing towards my future wages.");
		else
			say("I still hold " + mygold + " gold.");
		endif
	else
		say("You owe me " + mypay + " gold.");
	endif

	var mypack := findmypack(me.serial);
	foreach thingie in EnumerateItemsInContainer(mypack)
        	MoveItemToContainer(thingie, mstr.backpack);
	endforeach

endfunction

function statuscheck()

	var mypay := CInt(GetObjProperty(me, "mypay"));
	var mygold := CInt(GetObjProperty(me, "g"));

	if (mygold >= mypay)
		mypay := 0;
		mygold := mygold - mypay;
		say("I currently hold " + mygold + " gold for you.");
	else
		mypay := mypay - mygold;
		mygold := 0;
		say("You owe me " + mypay + " gold.");
	endif

	SetObjProperty(me, "mypay",mypay);
	SetObjProperty(me, "g", mygold);

endfunction

function GetPay()

	var mypay := CInt(GetObjProperty(me, "mypay"))+50;
	var mygold := CInt(GetObjProperty(me, "g"));
	var pastdue := CInt(GetObjProperty(me, "pastdue"));
	if (!pastdue)
		pastdue := 0;
	endif

	if (mygold >= mypay)
		mypay := 0;
		mygold := mygold - mypay;
	else
		mypay := mypay - mygold;
		mygold := 0;
	endif

	if (mypay)
		pastdue := pastdue +1;
		SetObjProperty(me, "pastdue",pastdue);
		if (pastdue > 2)
			me.hidden := 1;
			RevokePrivilege( me, "invul" );
			SetObjProperty(me, "guardkill", 1);
			Damage( me, GetHp(me) +3);
		endif
	else
		EraseObjProperty(me,"pastdue");
	endif

	SetObjProperty(me, "mypay",mypay);
	SetObjProperty(me, "g", mygold);

endfunction

function recontact(who)

        var cnt := SendTextEntryGump( who,
		"How shall I tell people to contact you? ",
		TE_CANCEL_DISABLE,
		TE_STYLE_NORMAL, 50);
        return cnt;

endfunction

function wear_item(item)

	foreach thing in ListEquippedItems(me)
        	MoveItemToContainer(thing, inv_pb);
	endforeach

	EquipItem(me, item);

	foreach thingie in EnumerateItemsInContainer(inv_pb)
        	EquipItem(me, thingie);
	endforeach

	foreach thingie in EnumerateItemsInContainer(inv_pb)
        	MoveItemToLocation(thingie, me.x, me.y, me.z, MOVEITEM_FORCELOCATION );
	endforeach

endfunction

function strip_items(mmaster)

	foreach thing in ListEquippedItems(me)
		if ((thing.objtype < STARTHAIR) || (thing.objtype > ENDHAIR))
	        	MoveItemToContainer(thing, inv_pb);
		endif
	endforeach

	foreach thing in EnumerateItemsInContainer(inv_fs)
		if (thing.container.serial == inv_fs.serial)
        		MoveItemToContainer(thing, inv_pb);
		endif
	endforeach

	foreach thingie in EnumerateItemsInContainer(inv_pb)
		if (thingie.container.serial == inv_pb.serial)
        		MoveItemToContainer(thingie, mmaster.backpack);
		endif
	endforeach

endfunction

function Fireme(ev)

	cashout();
	var mypay := CInt(GetObjProperty(me, "mypay"));
	var mygold := CInt(GetObjProperty(me, "g"));

	if (mypay)
		say("You can't fire me!");
	endif

	if (YesNo( ev.source, "Fire him?") )
		if (!mypay)
			strip_items(ev.source);
		endif
		me.hidden := 1;
		RevokePrivilege( me, "invul" );
		SetObjProperty(me, "guardkill", 1);
		Damage( me, GetHp(me) +3);
	endif

endfunction

function addthingstobuy()

	var buylist := {};
	var buyprice := { };
	var buyobj := { };
	var i;
	if (!GetObjProperty(me,"buylist"))
		buylist := {};
		buyprice := {};
		buyobj := { };
	else
		buylist := GetObjProperty(me,"buylist");
		buyprice := GetObjProperty(me,"buyprice");
		buyobj := GetObjProperty(me,"buyobj");
	endif

	say( "What do you want me to buy?" );
	var what := Target( master );
    	if ((!what) || (what == error) || (GetStrength(what)) )
		say( "Nevermind" );
		return;
    	endif

	if (what.desc in buylist)
		say("I will no longer purchase "+what.desc);
		var newbuylist := { };
		var newbuyprice := { };
		var newbuyobj := { };
		for (i := 1; i<=len(buylist); i:=i+1)
			if (buylist[i] != what.desc)
				newbuylist[len(newbuylist)+1] := buylist[i];
				newbuyprice[len(newbuyprice)+1] := buyprice[i];
				newbuyobj[len(newbuyobj)+1] := buyobj[i];
			endif
		endfor
		SetObjProperty(me,"buylist",newbuylist);
		SetObjProperty(me,"buyprice",newbuyprice);
		SetObjProperty(me,"buyobj",newbuyobj);
		return;
	endif

	if (!what.objtype)
		say("I can't buy those.  What the hell is that, anyway?");
		return;
	endif

	buylist[ len(buylist)+1] := what.desc;
	buyprice[ len(buyprice)+1] := CInt(SendTextEntryGump( master,
			"How much shall I pay for this item? "
, TE_CANCEL_ENABLE,
                        TE_STYLE_NORMAL));
	buyobj[ len(buyobj)+1] := what.objtype;

	if (len(buylist)==1)
		say("I will purchase "+buylist[1]);
	elseif (len(buylist)==2)
		say("I will purchase "+buylist[1] +" and "+ buylist[2]);
	else
		var buying := "I will purchase "+buylist[1];
		for (i := 2; i<=len(buylist)-1; i:=i+1)
			buying := buying + ", " +buylist[i];
		endfor
		buying := buying + " and " + buylist[len(buylist)];
		say(buying);
	endif

	SetObjProperty(me,"buylist",buylist);
	SetObjProperty(me,"buyprice",buyprice);
	SetObjProperty(me,"buyobj",buyobj);

endfunction

function SpoutBuyList(you)

	var buylist := {};
	var buyprice := {};
	var i;

	if (!GetObjProperty(me,"buylist"))
		say("I don't buy anything.");
		return;
	else
		buylist := GetObjProperty(me,"buylist");
		buyprice := GetObjProperty(me,"buyprice");
	endif

	for (i := 1; i<=len(buylist); i:=i+1)
		say("I'll pay "+ buyprice[i] + " for " + buylist[i]);
		sleep(2);
	endfor

	say( "Just hand me what you want me to buy." );

endfunction

function buyitem(you, it)

	var mypack := findmypack(me.serial);

	var itsownerserial := CInt(GetObjProperty(it, "ownerserial"));
	if (itsownerserial == CInt(master))
		say("I'll see that this is returned to my employer.");
		MoveItemToContainer(it,mypack);
		NotifyMaster(you.name + " gave me an item of yours.");
		return;
	endif

	var buylist := {};
	var buyprice := { };
	var buyobj := { };
	var i;
	if (!GetObjProperty(me,"buylist"))
		say("I don't buy anything");
		MoveItemToContainer(it,you.backpack);
		return;
	else
		buylist := GetObjProperty(me,"buylist");
		buyprice := GetObjProperty(me,"buyprice");
		buyobj := GetObjProperty(me,"buyobj");
	endif

	if (!(it.objtype in buyobj))
		say("Ugh, I don't want that.");
		MoveItemToContainer(it,you.backpack);
		return;
	endif

	var index := 0;
	for (i := 1; i<=len(buylist); i:=i+1)
		if (it.objtype == buyobj[i])
			index := i;
			break;
		endif
	endfor

	if (!index)
		say("Ugh, I don't want that.");
		MoveItemToContainer(it,you.backpack);
		return;
	endif

	var mygold := CInt(GetObjProperty(me, "g"));

	if ( (mygold-250) < (buyprice[index]*it.amount))
		say("Sorry, I can't afford that right now.");
		MoveItemToContainer(it,you.backpack);
		return;
	endif

	var itamount := it.amount;
	var itdesc := it.desc;
	if ( grabitem(it,mypack) )
		say("Thank you!  Here's your " + (buyprice[index]*itamount) + " gold.");
        	CreateItemInContainer(you.backpack, UOBJ_GOLD_COIN, buyprice[index]*itamount );
		NotifyMaster(you.name + " just sold me " + itdesc);
	else
		NotifyMaster("My backpack is full!");
		MoveItemToContainer(it,you.backpack);
		return;
	endif

	var theprice := buyprice[index]*itamount;
	var mypay := CInt(GetObjProperty(me,"mypay"));
	var commission := CInt( 0.05 * theprice);

	mygold := mygold - theprice;
	mypay := mypay + commission;

	if (mygold >= mypay)
		mypay := 0;
		mygold := mygold - mypay;
	else
		mypay := mypay - mygold;
		mygold := 0;
	endif

	SetObjProperty(me, "mypay",mypay);
	SetObjProperty(me, "g", mygold);

endfunction

function NotifyMaster(themessage)

	foreach onlinechr in EnumerateOnlineCharacters()
        if (onlinechr.serial == master)
		SendSysMessage(onlinechr, me.name + " : " + themessage);
		break;
        endif
	endforeach

endfunction

function TakeSale(goldamount, whoserial)

	var mypay := CInt(GetObjProperty(me, "mypay"));
	var mygold := CInt(GetObjProperty(me, "g"));
	var commission := CInt( 0.05 * goldamount);

	if (whoserial == master)
		if (!mypay)
			say("Since I work for you, I won't take a commission on that.");
			commission := 0;
		else
			say("Normally I wouldn't take commission on that, but you owe me "+ mypay + " gold!");
		endif
	endif

	mypay := mypay + commission;
	goldamount := goldamount - commission;
	mygold := mygold + goldamount;

	if (mygold >= mypay)
		mypay := 0;
		mygold := mygold - mypay;
	else
		mypay := mypay - mygold;
		mygold := 0;
	endif

	SetObjProperty(me, "mypay",mypay);
	SetObjProperty(me, "g", mygold);

endfunction

function TakeGold(ev)

	var goldamount := ev.item.amount;
	var mypay := CInt(GetObjProperty(me, "mypay"));
	var mygold := CInt(GetObjProperty(me, "g"));

	if (mypay)
		if (goldamount >= mypay)
			goldamount := goldamount - mypay;
			mypay := 0;
		else
			mypay := mypay - goldamount;
			goldamount := 0;
			say("You still owe me " + mypay + " gold.");
		endif
	endif

	if (goldamount)
               	mygold := mygold + goldamount;
		say("I am holding " + mygold + " gold for you.");
	endif

	SetObjProperty(me, "g", mygold);
	SetObjProperty(me,"mypay",mypay);

	DestroyItem(ev.item);

endfunction

function HireMe(ev)

	say("I would be honored to take your service");

	master := ev.source.serial;
	mastername := ev.source.name;
	cont := recontact(ev.source);

	SetObjProperty(me, "master", ev.source.serial);
	SetObjProperty(me, "mn", ev.source.name);

	var mygold := 0;
	if (ev.item.amount > 1000)
		mygold := ev.item.amount - 1000;
	endif

	SetObjProperty(me, "g", mygold);
	SetObjProperty(me, "mypay",0);

	DestroyItem(ev.item);
	var newname := SplitWords(me.name);
	me.name := newname[1] + ", " + mastername + "'s Merchant";

endfunction

function grabitem(it,mypack)

	var mystuff := EnumerateItemsInContainer(mypack);

	if (it.amount > 1 )
	  foreach item in mystuff
		if (item.objtype == it.objtype)
		  if (CreateItemInContainer( mypack, it.objtype, it.amount ))
		  	DestroyItem(it);
			return 1;
		  else
			say("My backpack is full!");
			return 0;
		  endif
		endif
	  endforeach
	else
	  foreach item in mystuff
		if ((item.objtype == it.objtype) && (item.amount > 1 ))
		  if ( CreateItemInContainer( mypack, it.objtype, it.amount ) )
		  	DestroyItem(it);
			return 1;
		  else
			say("My backpack is full!");
			return 0;
		  endif
		endif
	  endforeach
	endif

	if (MoveItemToContainer(it,mypack))
		return 1;
	else
		return 0;
	endif

endfunction