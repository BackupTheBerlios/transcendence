use npc;
use os;
use uo;
use cfgfile;

include "include/speech";
include "include/client";
include "include/eventid";
include "include/randname";
include "include/dist";
include "include/anchors";
include "ai/main/loot";
include "ai/combat/fight";
include "ai/combat/defaultcombatevent";
include "include/vetement";

var configfile := ReadConfigFile("::npcdesc");
var wpncfg := ReadConfigFile(":combat:itemdesc");
var flee_point := 0;

// TODO: move these to include/client.inc
const UACTION_LOOK_AROUND   := 0x05;
const UACTION_SALUTE        := 0x21;

const HALT_THRESHOLD := 10; // how close before he barks?
var speech := 99;
var me := Self();

if( me.name["<random>"] )
		SetName( me, RandomName( me ) );
	   set_priority(50);
       Habille( me, GetObjProperty(me,"Equipt") );
       //EquipFromTemplate( me, GetObjProperty(me,"Equipt") );
       set_priority(1);
endif

	if (!me.backpack)
		var newbackpack := CreateItemAtLocation(me.x, me.y, me.z, 0xe75,1, me.realm);
		EquipItem(me, newbackpack);
	endif


program CastleGuard()

	var npccfg := ReadConfigFile("npcdesc");
        var speechelem := npccfg[me.npctemplate];
        speech := GetConfigString(speechelem, "speech");

    EnableEvents( EVID_ENGAGED + EVID_DISENGAGED + EVID_DAMAGED );
    EnableEvents( EVID_ENTEREDAREA + EVID_LEFTAREA, HALT_THRESHOLD );
    EnableEvents( EVID_OPPONENT_MOVED );
    EnableEvents( EVID_ITEM_GIVEN );
    EnableEvents( EVID_SPEECH, 4);

    SetWarMode( 0 );
    set_priority(100);

    var ev;
	var evtext;
    var next_wander := ReadGameClock() + 10;
    var wanders := 9;
    while (1)
        ev := os::wait_for_event(5);
	if (ev)
        case (ev.type)
            EVID_SPEECH:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		evtext := lower(ev.text);
		if ( (evtext["guards"]) || (evtext["guards!"]) )
			look_around();
		endif
                check_speech(ev.text, ev.source);
                RespondToSpeech(me,ev.text);
            EVID_ENGAGED:
            EVID_DAMAGED:
		wanders := 0;
		next_wander := ReadGameClock()+60;
                if (ev.source)
		    say("Die " + ev.source.name + "!" );
		    AddToHateList( ev.source );
                    Fight( ev.source );
                endif
            EVID_ENTEREDAREA:
		wanders := 0;
                PerformAction( me, UACTION_LOOK_AROUND );
		look_around();
            EVID_ITEM_GIVEN:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		TakeItem(ev.source,ev.item);
        endcase
	endif

       if (ReadGameClock() >= next_wander)
		wanders:=wanders+1;
		wander();
                next_wander := ReadGameClock() + 10;
		if (wanders>10)
			sleepmode();
			wanders := 0;
		endif
        endif

    endwhile
endprogram

function CloseDistance( opponent )
    case Distance( me, opponent )
        1:              // the most likely, so first 
        0:
            return 1;
        default:
            RunToward( opponent );
            return 0;
    endcase
endfunction

function look_around()

        foreach critter in ListMobilesNearLocation(me.x, me.y, me.z, 15, me.realm)
                if ((critter.npctemplate) && (critter.script != "tamed") )
                	
                	if (!configfile[critter.npctemplate].guardignore)
						say("Ah ha! " + critter.name);
                                                Fight(critter);
                	endif
	        else
					if(GetObjProperty(critter, "DarkAura"))
					 say("Go away from here, sacrilegue creature!!");
					 fight(critter);
					endif
					CheckForCriminals(critter);
              	endif
        endforeach

endfunction

function sleepmode()

	foreach critter in ListMobilesNearLocation( me.x,me.y,me.z, 32, me.realm )
          if (!critter.isA(POLCLASS_NPC))
		return 0;
          endif
	endforeach

	var ev;

	while (1)
		ev := os::wait_for_event( 120 );

		repeat
		if (ev)
		case (ev.type)
                	EVID_ENGAGED:
			EVID_DAMAGED:
			EVID_ENTEREDAREA:
				if (!ev.source.isA(POLCLASS_NPC))
					CheckForCriminals(ev.source);
					return 0;
				endif
		endcase
		endif
		until (! (ev := os::wait_for_event(120)) );

	endwhile     

endfunction      

function CheckForCriminals(jerk)

	if (jerk.criminal)
		say("Stop Criminal!");
		AddToHateList( jerk );
		Fight(jerk);
	elseif ( CheckHateList(jerk) )
		say("You're not welcome in this town, "+ jerk.name );
		Fight(jerk);
	endif

endfunction

function AddToHateList(crim)

	if (crim.isA(POLCLASS_NPC))
		return;
	endif

	var crooks := { };
	if (!GetObjProperty(me,"crooks") )
		crooks[1] := crim.serial;
		SetObjProperty(me,"crooks",crooks);
		return;
	else
		crooks := GetObjProperty(me,"crooks");
	endif

	var i;
	foreach crook in crooks;
		if ( crook == crim.serial )
			return;//already hate him.
		endif
	endforeach

	if (len(crooks)>3)
		crooks[ RandomInt(3)+1 ] := crim.serial;
	else
		crooks[ len(crooks)+1 ] := crim.serial;
	endif

	SetObjProperty(me,"crooks",crooks);

endfunction

function CheckHateList( you )

	if (you.isA(POLCLASS_NPC))
		return;
	endif

	var crooks := { };
	if (!GetObjProperty(me,"crooks") )
		return 0;//can't be on my list cause I don't have one
	else
		crooks := GetObjProperty(me,"crooks");
	endif


	var i;
	foreach crook in crooks;
		if ( crook == you.serial )
			return 1;//hate you.
		endif
	endforeach

	return 0;

endfunction

function cleareventque()

	var ev;
	repeat
	until (! (ev := os::wait_for_event(0)) );

endfunction

function EnableMainEvents()

    cleareventque();
    DisableEvents( EVID_SPEECH + EVID_LEFTAREA + EVID_DISENGAGED + EVID_OPPONENT_MOVED );
    EnableEvents( EVID_SPEECH + EVID_ENGAGED + EVID_DAMAGED, HALT_THRESHOLD );
    EnableEvents(EVID_ITEM_GIVEN);

endfunction

function DisableMainEvents()

    cleareventque();
    DisableEvents( EVID_SPEECH + EVID_ENGAGED + EVID_DAMAGED );
    DisableEvents(EVID_ITEM_GIVEN);

endfunction