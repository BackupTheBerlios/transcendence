///////////////////////////////////////////////
// chicken.src
//
// Created 7/26/2002 11:31AM
// Updated 2/14/2003 9:04AM
//
// By Austin Heilman
// http://www.tsse.net
//
///////////////////////////////////////////////
//
// This nerve causes the npc to wander around
// and make idle noises just like graze does
// but it also has an extra bit for causing the
// npc to create eggs every so often.
//
///////////////////////////////////////////////

use uo;
use os;
use util;

include ":ai_brain:ai_include/NPC_Commands";

program WanderNerve(params)
	var nerve_name	:= params[1];
	var npc		:= params[2];
	var ev		:= params[3];
	var settings	:= params[4];
	
	while(npc)
		npc.facing := RandomInt(8);
		AI_Wander(npc, NOWAKE);
		
		sleep(20+RandomInt(10));
		
		if ( settings["idle_sound"] )
			if ( settings["idle_sound"].size() > 0 )
				var sounds := settings["idle_sound"];
				PlaySoundEffect(npc, sounds[RandomInt(sounds.size())+1]);
			else
				PlaySoundEffect(npc, settings["idle_sound"]);
			endif
		endif
		
		if ( CInt(GetObjProperty(npc, "NextLay")) < polcore().systime )
			CreateItemAtLocation(npc.x, npc.y, npc.z+1, "eggs", 1, npc.realm);
			SetObjProperty(npc, "NextLay", polcore().systime+21600);
		endif
		
		AI_ClearThoughts(npc, CLR_NERVE);
	endwhile
endprogram
