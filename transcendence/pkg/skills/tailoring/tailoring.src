/////////////////////////////////////////////////////////////////////////////
//
//  tailoring.src - I think this just turns thread into cloth at a loom?
//
//
//
/////////////////////////////////////////////////////////////////////////////

use uo;
use os;

include "include/client";
include "include/objtype";



function MakeCloth( character, thread )

  // Test to see if the player can reach the targed thread.
  if (not Accessible( character, thread ))
    SendSysMessage( character, "You don't have any thread!" );
    return;
  endif

  if( thread.amount >= 5 )    //
                              //
    set_critical( 1 );        //  These lines combat a moved-materials exploit.
                              //
    if( thread.amount >= 5 )  //
       // Create one bolt of cloth in the players backpack
       var cloth := CreateItemInBackpack( character, UOBJ_CLOTH, 1 );
       if (cloth)
          // if the creation of the cloth item was successfull.
          // Subtract five spools of thread from the players inventory
          SubtractAmount( thread, 5 );
          SendSysMessage( character, "You weave some thread and create a bolt of cloth" );
       endif
    else
       SendSysMessage( character, "You don't have enough thread for the bolt of cloth.");
    endif
  else
       SendSysMessage( character, "You don't have enough thread for the bolt of cloth.");
  endif

endfunction

program use_thread( character, thread )
  SendSysMessage( character, "Select the loom to use that on" );

  // Ask the player what they wish to use the thread on.
  var use_on := Target( character, "" );

  // If they cancel, quit.
  if (!use_on)
    return;
  endif

  // Get the type of object.
  var obj_type := GetObjType(use_on);

  if ( (obj_type < UOBJ_LOOM_START) || (obj_type > UOBJ_LOOM_END))
    // If the object is NOT a loom, give the player an error message.
    SendSysMessage( character, "You can only use thread on a loom" );
  else
    // Else, try and make the cloth
    MakeCloth( character, thread);
  endif
endprogram
