/////////////////////////////////////////////////////////////////////////////
//
//  Tailoring: Original Code by Racalac
//  Updated: 8/25/1999   By: Sentack
//  TODO: Move all the UOBJ objects to "include/objtype.inc"
//
//  NOTES: Lines 129 and 196 are optional statments that are designed to
//    replace lines 127 and 194.  They are used to remove a random amount of
//    cloth from the players inventory instead of just one unit of cloth
//    as does the OSI way of dealing with failer.  Sometimes you lose a little
//    while other times you lose a lot.  But never more then the amount of 
//    cloth the object original was going to take.
/////////////////////////////////////////////////////////////////////////////

use uo;
use cfgfile;
use os;
use util;

include "include/client";
include "include/attributes";

const UOBJ_SEWING_KIT   := 0x0f9d;
const UOBJ_BOLT_START   := 0x0f95; // Bolts <Initial Tile>
const UOBJ_BOLT_END     := 0x0f9c; // Bolts <Final Tile>
const UOBJ_CLOTH_STARTS := 0x175d; // Real cloth
const UOBJ_CLOTH_END    := 0x1768; // Real cloth
const UOBJ_MUTLI_CLOTH  := 0x1765; // What we will turn bolts into after they have been used.

const UOBJ_HIDES_START  := 0x1078;
const UOBJ_HIDES_END    := 0x1079;

const UOBJ_ONYX_HIDE    := 0x7020;
const UOBJ_DRAGON_HIDE  := 0x7021;  // Red hide
const UOBJ_GREEN_HIDE   := 0x7022;  // Malachite green
const UOBJ_AZURE_HIDE   := 0x7023;  // Blue
const UOBJ_OLIVE_HIDE   := 0x7024;
const UOBJ_SAND_HIDE    := 0x7025;  // Sandy-Grey hide

const SCISSORS_SOUND    := 0x0249;

var tailor_cfg       := ReadConfigFile( "tailoring" );
var leather_cfg      := ReadConfigFile( "leather" );

function MakeClothItems( character, cloth )
  var skill;         // Amount of skill required to perform the task
  var element;       // The item the player wishes to create
  var material;      // The amount of material it will take to make the item
  var retval;        // Stores a value but never used.  Stores the result of Playing Sound Effects
  var mainclothused; // Pointer to the cloth being used in the end.
  var boltcount;     // Total number of bolts available, if any
  var totalcloth;    // How much cloth do we have total

  // Determine if the user does have access to the cloth
  if (not Accessible( character, cloth))
    SendSysMessage( character, "You dont have any cloth!" );
    return;
  endif

  // Open up the tailor config file.
  if (!tailor_cfg)
    SendSysMessage(character, "Could not open tailoring.cfg");
    syslog( "Unable to open tailoring.cfg" );
    return;
  endif

  // Ask the user what they would like to make
  var what := SelectMenuItem2( character, "tailoring" );

  // Determine exactly what the player selected to make.  <Sentack: What if they try and cancel out now?>
  element := FindConfigElem( tailor_cfg, what.objtype );

  // Determine the material and skill required to make the item and inform the player
  if (element)
    material := GetConfigInt(element, "material");
    skill    := GetConfigInt(element, "skill");
    SendSysMessage(character, "(" + material + " yards of cloth and " + skill + " skill)");
  endif

  if ((cloth.objtype >= UOBJ_BOLT_START) && (cloth.objtype <= UOBJ_BOLT_END))
    totalcloth := cloth.amount * 50;  // Number of bolts * 50 Yards each.
  else
    totalcloth := cloth.amount;  // Just the amount
  endif
 
  if (what.objtype) // if the user did pick an object, then we are going to use cloth

    if (totalcloth >= material )
     
      //bandages use all the cloth
      if (what.objtype == 0x0e21)
	material := (totalcloth);
      endif

      if ((cloth.objtype >= UOBJ_BOLT_START) && (cloth.objtype <= UOBJ_BOLT_END))
        boltcount := cloth.amount;
        DestroyItem(cloth);
        mainclothused := CreateItemInBackpack(character, UOBJ_MUTLI_CLOTH, boltcount * 50);
      else
        mainclothused := cloth;
      endif
 
      if ( CheckSkill( character, SKILLID_TAILORING, skill, skill*4 ) )
        makenoise(character,retval);
       
        if (what.objtype == 0x0e21)
	  var theitem := CreateItemInBackpack(character, what.objtype, CInt(material/2));
	else
          var theitem := CreateItemInBackpack(character, what.objtype, 1);
	endif

        SubtractAmount(mainclothused, material);
        SendSysMessage(character, "You create the item and place it in you pack.");
	if (mainclothused.amount)
          SendSysMessage(character, "Cloth remaining: " + mainclothused.amount);
	endif
      else
        SendSysMessage(character, "You failed, ruining some cloth.");

	//failure on bandages only ruins 2 cloth
	if (what.objtype == 0x0e21)
		material := 2;
      	endif

	SubtractAmount(mainclothused, (material/2)+1);
	if (mainclothused.amount)
          SendSysMessage(character, "Cloth remaining: " + mainclothused.amount);
	endif
      endif

    else
      SendSysMessage(character, "Not enough cloth.");
    endif

  else
    return;
  endif

endfunction


function MakeLeatherItems(character, leather)
  var skill;
  var element;
  var material;
  var retval;

  if (not Accessible( character, leather))
    SendSysMessage( character, "You dont have any leather!" );
    return;
  endif

  if (!leather_cfg)
    SendSysMessage(character, "Could not open leather.cfg");
    syslog( "Unable to open leather.cfg" );
    return;
  endif

  var what := SelectMenuItem2( character, "leatherworking" );

  element := FindConfigElem( leather_cfg, what.objtype );

  if (element)
    material := (CInt(GetElemProperty(element, "material" )));
    skill := CInt(GetElemProperty( element, "skill" ));
    SendSysMessage(character, "(" + material + " hides and " + skill + " skill)");
  endif

  if (GetAmount(leather) >= material )
    if (CheckSkill( character, SKILLID_TAILORING, skill, skill*4 )) 
      makenoise(character,retval);
      var theitem := CreateItemInBackpack(character, what.objtype, 1);
      if (theitem)
                      if (GetEffectiveSkill(character, SKILLID_TAILORING) > 90)
                        if (RandomInt(20) == 1)
                                theitem.quality := 1.5;
                                theitem.hp := theitem.maxhp;
                                theitem.name := theitem.desc + " (Exceptional)";
                                SendSysMessage(character, "You've made an exceptional item!");
                        else
                                theitem.quality := (60 + (RandomInt(GetEffectiveSkill(character, SKILLID_TAILORING))/2))/100.0;
                                theitem.hp := theitem.maxhp;
                        endif
                else
                                theitem.quality := (60 + (RandomInt(GetEffectiveSkill(character, SKILLID_TAILORING))/2))/100.0;
                                theitem.hp := theitem.maxhp;
                endif
        theitem.color := leather.color;
	SubtractAmount( leather, material );
        SendSysMessage(character, "You create the item and place it in you pack.");
      elseif ( !(what.objtype) )
        return;
      endif
    else
      SendSysMessage(character, "You failed, ruining some leather.");
      SubtractAmount(leather, RandomInt(material+1)); 
    endif
  else
    SendSysMessage(character,"You dont have enough hides for that");
  endif

endfunction


program use_sewing_kit( character, sewing_kit )
  SendSysMessage( character, "What cloth do you want to use?" );
 
  // Ask the user for the target item be it cloth or leather.
  var use_on := Target( character, TGTOPT_CHECK_LOS );
  var his_skill := GetEffectiveSkill( character, SKILLID_TAILORING );
 
  // If the user aborts, then quit
  if (!use_on)
    return;
  endif

  // Get the object type.
  var obj_type := use_on.objtype;
  ReserveItem(use_on);
  if ( ((obj_type >= UOBJ_BOLT_START)  && (obj_type <= UOBJ_BOLT_END)) ||   // Bolts
       ((obj_type >= UOBJ_CLOTH_STARTS) && (obj_type <= UOBJ_CLOTH_END)) )  // stacks of cloth
    // if it's of cloth type, then being the clothing process
    MakeClothItems(character, use_on);
 
  elseif ( (obj_type == UOBJ_HIDES_START) || (obj_type == UOBJ_HIDES_END) )
    // if it's of Leather type, then begin the leather process
    MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_SAND_HIDE) && ( his_skill >= 70 ) )
	MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_OLIVE_HIDE) && ( his_skill >= 80 ) )
	MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_GREEN_HIDE) && ( his_skill >= 85 ) )
	MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_ONYX_HIDE) && ( his_skill >= 90 ) )
	MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_AZURE_HIDE) && ( his_skill >= 95 ) )
	MakeLeatherItems(character, use_on);
  elseif ( (obj_type == UOBJ_DRAGON_HIDE) && ( his_skill >= 99 ) )
	MakeLeatherItems(character, use_on);
 
  elseif ( (obj_type == UOBJ_SAND_HIDE) && ( his_skill <= 70 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  elseif ( (obj_type == UOBJ_OLIVE_HIDE) && ( his_skill <= 80 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  elseif ( (obj_type == UOBJ_GREEN_HIDE) && ( his_skill <= 85 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  elseif ( (obj_type == UOBJ_ONYX_HIDE) && ( his_skill <= 90 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  elseif ( (obj_type == UOBJ_AZURE_HIDE) && ( his_skill <= 95 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  elseif ( (obj_type == UOBJ_DRAGON_HIDE) && ( his_skill <= 99 ) )
	SendSysMessage( character, "You have no chance to successfully work with that material." );
  else
    // else warn the user that they made a mistake on there selection.
    SendSysMessage(character, "You can only use that on cloth!");
    return;
  endif
endprogram

function makenoise(character,retval)

      retval := PlaySoundEffect(character, SCISSORS_SOUND);
      sleep(1);
      retval := PlaySoundEffect(character, SCISSORS_SOUND);
      sleep(1);
      retval := PlaySoundEffect(character, SCISSORS_SOUND);
      sleep(1);
      retval := PlaySoundEffect(character, SCISSORS_SOUND);
      sleep(1);

endfunction
