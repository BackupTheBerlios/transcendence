use uo;
use os;
use util;
use cfgfile;
use polsys;

include "include/spellRestrictions";
include "include/attributes";
include "include/particles";
include "include/effects2D";
include "checkeffects";

var spellscfg := ReadConfigFile(":spells:spells"); 

program cast_summon( parms )
  var circle := 5;
  var delivery := "indirect";
  var noto := "harmful";
  var result := maincast(parms, delivery, circle, noto, 33);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  var thecritter := CreateNpcFromTemplate("bladespirit", cast_on.x, cast_on.y, cast_on.z, 0, cast_on.realm);
  PlaySoundEffect(thecritter,SFX_SPELL_BLADE_SPIRITS);
  SetObjProperty(thecritter, "summoned",1);
  SetObjProperty(thecritter, "master", caster.serial);
  Detach();
  sleep( (GetEffectiveSkill( caster, SKILLID_MAGERY ) ) );
  PlaySoundEffect( thecritter, SFX_SPELL_MANA_VAMPIRE );
  PlayStationaryEffect( thecritter.x, thecritter.y, thecritter.z, FX_SMOKE, 0xa, 0xa, 0, thecritter.realm );
  MoveCharacterToLocation(thecritter,0,0,0,MOVECHAR_FORCELOCATION);
  ApplyRawDamage(thecritter, GetHp(thecritter) + 3);
endprogram

