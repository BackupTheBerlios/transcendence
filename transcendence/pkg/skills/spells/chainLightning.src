use uo;
use os;
use util;
use cfgfile;
use polsys;

include "include/spellRestrictions";
include "include/particles";
include "include/effects2D";
include "checkeffects";
include "include/AOSPackets";



program cast_chainlightning( parms )
  var spellscfg := ReadConfigFile(":spells:spells"); 
  var circle := 7;
  var delivery := "indirect";
  var noto := "harmful";
  var result := maincast(parms, delivery, circle, noto, 49);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  var dmg;
  var range := 2;
  var victims := ListMobilesNearLocationEx( cast_on.x, cast_on.y, cast_on.z, range, 0x03, cast_on.realm);
  Detach();
  foreach mobile in victims
    if((CheckLineOfSight(mobile, caster)) and (mobile.cmdlevel < 2))
      SetObjProperty(mobile, "LastHit", {caster.name, caster.serial, "chain lightning"});
      if(Reflected(mobile))
        mobile := caster;
      endif
      dmg := RandomDiceRoll("6d8");
      send_attack(mobile,caster, 49);
      
      //if(GetObjProperty (caster, "SessionClientType") == "3D")
         CheckForSpell3DEffects(spellscfg, caster, mobile, 49);
      //else
         PlayLightningBoltEffect( mobile );
      //endif
      
      PlaySoundEffect(mobile, SFX_SPELL_LIGHTNING);
      if(mobile.isA(POLCLASS_NPC))
        dmg := dmg * 2;
      endif
      ApplyRawDamage( mobile, Resisted(circle,caster,mobile,dmg) );
      if(!caster.npctemplate)
       SendVisualHPloss(caster, mobile, dmg);
      endif
      sleepms(600);
    endif
  endforeach
endprogram