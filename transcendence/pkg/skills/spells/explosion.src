use os;
use uo;
use cfgfile;
use polsys;

include "include/spellRestrictions";
include "include/attributes";
include "include/particles";
include "include/effects2D";
include "checkeffects";
include "include/AOSPackets";

var spellscfg := ReadConfigFile(":spells:spells"); 

program cast_explosion( parms )
  var circle := 6;
  var delivery := "direct";
  var noto := "harmful";
  var result := maincast(parms, delivery, circle, noto, 43);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  var dmg;
  SetObjProperty(cast_on, "LastHit", ( {caster.name, caster.serial, "explosion" } ) );
  if(Reflected(cast_on))
	cast_on := caster;
  endif
  dmg := RandomDiceRoll("7d6");
  Detach();
  sleep(4);
  send_attack(cast_on,caster, 43);
  PlaySoundEffect( cast_on, SFX_SPELL_EXPLOSION );
  CheckForSpell3DEffects(spellscfg, caster, cast_on, 43);
  Play2DStationaryEffectHuefx( cast_on.x, cast_on.y, cast_on.z, FX_EXPLODE_3, 7, cast_on.realm, 0x10);
  //PlayStationaryEffect( cast_on.x,cast_on.y,cast_on.z, FX_EXPLODE_3, 7, 0x10 );
  if(GetHp(cast_on) > 1)
    if(cast_on.npctemplate)
      dmg := dmg * 2;
    endif
    ApplyRawDamage(cast_on, Resisted(circle,caster,cast_on,dmg));
    if(!caster.npctemplate)
     SendVisualHPloss(caster, cast_on, dmg);
    endif
  endif
endprogram