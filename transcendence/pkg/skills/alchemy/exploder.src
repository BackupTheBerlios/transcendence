// Purple potion  version .1
// by Zulu (zulu@zuluhotel.com)   May 1, 2000

use uo;
use cfgfile;
use os;
use util;

include "include/checkcity";
include "include/attributes";

const MOVING_EFFECT_PURPLE_POTION  := 0x0f0d;
const SOUND_EFFECT_EXPLOSION	   := 0x208;
const EFFECT_EXPLOSION             := 0x36b0;
program exploder(parms)

var potions := {0xdc08, 0xdc09, 0xdc07};

    var itemdef := ReadConfigFile("itemdesc");

    if (!itemdef)
        return;
    endif

    var telem := itemdef[parms[1].objtype];
    var potion_strength := GetStrength(telem);
    var thecenter := parms[1];
   
    while (thecenter.container)
        thecenter := thecenter.container;
    endwhile
   
     SetScriptController( parms[2] );
     PlayObjectCenteredEffect( parms[1], EFFECT_EXPLOSION, 7, 0x10 );
     parms[1].graphic := 1;

     var dmg := ( RandomInt(19) + 1 ) * potion_strength;
       
     if (checkcity(thecenter))
        dmg := (dmg/2);
     else
        dmg := dmg + 2;
     endif
    
     foreach critter in ListMobilesNearLocationEx( thecenter.x,thecenter.y,thecenter.z, potion_strength, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN, parms[2].realm );
        PlayObjectCenteredEffect( critter, EFFECT_EXPLOSION, 7, 0x10 );
        PlaySoundEffect( critter,  SOUND_EFFECT_EXPLOSION );
        ApplyRawDamage(critter, dmg ); // note this is raw damage
     endforeach
    
     foreach item in ListItemsNearLocation( thecenter.x,thecenter.y,thecenter.z, potion_strength, parms[2].realm)
        if (item.objtype in potions && item.serial != parms[1].serial)
                var parm := array;
                parm[1] := item;
                parm[2] := parms[2];
                start_script(":alchemy:exploder", parm);
        endif
     endforeach
     DestroyItem(parms[1]);    
endprogram