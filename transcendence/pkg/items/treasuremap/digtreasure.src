//////////
// digtreasure.src
//
// Fires when the tool that was created when you decode a treasuremap 
// is double clicked. Target the map you want to dig from and then a
// ground tile. If you are close enough to the chest coords (chestx, y, z
// in the map) according to your mining skill, a chest is created and
// yummy loot is placed in it according to the map's level. The chest 
// should be locked and trapped. The map and the digging tool are 
// deleted after the chest is 'found'. The chest decays in 30 minutes.
//
// Author: Racalac, 4/24/99
// Hint system added by Zulu, zuluhotel@home.com  01/02/2000
// 
/////////


use uo;
use os;
use cfgfile;
use util;

//set_debug(1);

include "include/client";
include "include/attributes";
include "include/dist";
include "include/objtype";
include "include/starteqp";

const UOBJECT_TREASURE_CHEST := 0x0e40;

program dig_treasure( character, tool )

    SendSysMessage( character, "Target a treasure map to dig from." );
    var map := Target( character, "" );

    if(!map)
       SendSysMessage( character, "Cancelled." );
       return;
    endif

    if( map.objtype != UOBJ_MAP1 )
       SendSysMessage( character, "You must select a treasure map." );
       return;
    endif

    var chestx := GetObjProperty( map, "chestx" );
    var chesty := GetObjProperty( map, "chesty" );
    var chestz := GetObjProperty( map, "chestz" );

    if( (!chestx) || (!chesty) )  //z might be 0!
       SendSysMessage( character, "That is not a treasure map." );
       return;
    endif

    if( character.serial !=  (GetObjProperty( map, "decoder" ) ) )
       SendSysMessage( character, "You must have decoded the map to dig for it." );
       return;
    endif

    SendSysMessage( character, "Target a location to dig." );
    var dig := TargetCoordinates( character );

    if(!dig)
       SendSysMessage( character, "Cancelled." );
       return;
    endif

    if(!CheckLosAt( character, dig.x, dig.y, dig.z ) );
       SendSysMessage( character, "You can't see that." );
       return;
    endif
 
    var range := GetRange( character );
    var distn := coordist( dig.x, dig.y, chestx, chesty );
    var dirns := "None";
    var direw := "None";

    if( distn > range )

	if ( dig.y > chesty )
	 dirns := "North";
	endif
	if ( dig.y < chesty )
	 dirns := "South";
	endif
	if ( dig.x < chestx )
         direw := "East";
	endif
	if ( dig.x > chestx )
         direw := "West";
	endif

	if (direw == "None")
		SendSysMessage( character, "You dig for awhile and have a feeling to travel to the " + dirns + "." );
	else
		SendSysMessage( character, "You dig for awhile and have a feeling to travel to the " + dirns + ", " + direw + "." );
	endif

    else
       // s/gfx
       var chest := CreateTreasureChest( map, chestx, chesty, chestz );
       SendSysMessage( character, "You find the treasure!" );
       DestroyItem( map );
       DestroyItem( tool );

       Detach();
       sleep(1800);
       DestroyItem(chest);
    endif

endprogram


function GetRange( character );

    var skill := GetEffectiveSkill( character, SKILLID_MINING );

    if( skill >= 100 )
       return 10;
    elseif( skill >= 90 )
       return 9;
    elseif( skill >= 80 )
       return 8;
    elseif( skill >= 70 )
       return 7;
    elseif( skill >= 60 )
       return 6;
    elseif( skill >= 50 )
       return 5;
    elseif( skill >= 40 )
       return 4;
    elseif( skill >= 30 )
       return 3;
    elseif( skill >= 20 )
       return 2;
    else
       return 1;
    endif

endfunction


function CreateTreasureChest( map, x, y, z, realm := "britannia" )

   //TODO: lock and trap the chest!

   var chest := CreateItemAtLocation( x, y, z, UOBJECT_TREASURE_CHEST, 1, realm );
   var lvl := GetObjProperty( map, "level" );
   var lootgroup;

   case lvl
      1: lootgroup := 5;
      2: lootgroup := 6;
      3: lootgroup := 7;
      4: lootgroup := 8;
      5: lootgroup := 9;
      default: lootgroup := 5;
   endcase

   SetObjProperty( chest, "level", lvl );
   chest.movable := 0;

   chest.usescript := ":treasuremap:treasurechest";

   MakeLoot( chest, lootgroup );

   return chest;

endfunction