use uo;
use os;

include "include/packets";

program reconnect(who)
// AOS/LBR Packet Addon
  var acct := who.acct;
  if(acct.GetProp("AOS"))
    SendPacket(who, "B9801B");
  elseif(acct.GetProp("LBR"))
    SendPacket(who, "B90003");
  endif
// Seasons addon
  var gseason := GetGlobalProperty("gseason");
  case(gseason)
      "SEASON_DESOLATION": SetSeasonForPlayer( who, SEASON_DESOLATION);
      "SEASON_WINTER":     SetSeasonForPlayer( who, SEASON_WINTER);
      "SEASON_FALL":       SetSeasonForPlayer( who, SEASON_FALL);
      "SEASON_SUMMER":     SetSeasonForPlayer( who, SEASON_SUMMER);
      "SEASON_SPRING":     SetSeasonForPlayer( who, SEASON_SPRING);
  endcase
// End Packet Addons
  var mount := GetEquipmentByLayer(who, 25 );
  if (mount)
    var animal := SystemFindObjectBySerial(CInt(GetObjProperty(mount,"serial")));
    EraseObjProperty(animal,"onhold");
  endif
  run_script_to_completion( ":motd:textcmd/player/motd", who );
  var ppl := EnumerateOnlineCharacters();
  if (GetObjProperty(who, "StealthLogin") )
    return;
  endif
  foreach staff in ppl
    if((staff.cmdlevel > 0) && (CInt(GetObjProperty(staff, "LogNotify") ) == 1) )
      SendSysMessage(staff, CStr(who.name) + " (Account name: " + CStr(who.acctname) + ") has reconnected.  IP: " + CStr(who.ip) );
    endif
  endforeach
endprogram
